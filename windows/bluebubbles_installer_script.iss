; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "BlueBubbles"
#define MyAppVersion "1.15.5.0"
#define MyAppPublisher "BlueBubbles"
#define MyAppURL "https://bluebubbles.app/"
#define MyAppExeName "bluebubbles_app.exe"
#define ProjectRoot ".."

#include "CodeDependencies.iss"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{6129D070-FCBC-4167-8C1F-9A4B18263EFF}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog
OutputDir=.
OutputBaseFilename=bluebubbles-windows
SetupIconFile={#ProjectRoot}\assets\icon\icon.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Code]
function InitializeSetup: Boolean;
begin
  Dependency_AddVC2015To2022();
  Dependency_AddWebView2();
  Result := True;
end;

[Dirs]
Name: "{app}\bluebubbles_app.exe.WebView2"; Permissions: users-modify

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#ProjectRoot}\build\windows\x64\runner\Release\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ProjectRoot}\build\windows\x64\runner\Release\*.lib"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ProjectRoot}\build\windows\x64\runner\Release\*.exp"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ProjectRoot}\build\windows\x64\runner\Release\*.dll"; DestDir: "{app}"; Flags: ignoreversion
// Source: "{#ProjectRoot}\windows\dlls\*.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ProjectRoot}\build\windows\x64\runner\Release\data\*"; DestDir: "{app}\data"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

; Having old versions of these causes crashes for some users
[InstallDelete]
Type: files; Name: "{app}\api-ms-win-*.dll"
Type: files; Name: "{app}\concrt140.dll"
Type: files; Name: "{app}\libc++.dll"
Type: files; Name: "{app}\media_kit_native_event_loop.dll"
Type: files; Name: "{app}\msvcp140*.dll"
Type: files; Name: "{app}\screen_brightness_windows_plugin.dll"
Type: files; Name: "{app}\ucrtbase.dll"
Type: files; Name: "{app}\ucrtbased.dll"
Type: files; Name: "{app}\vccorlib140.dll"
Type: files; Name: "{app}\vccorlib140d.dll"
Type: files; Name: "{app}\vcruntime140.dll"
Type: files; Name: "{app}\vcruntime140_1.dll"
Type: files; Name: "{app}\vcruntime140_1d.dll"
Type: files; Name: "{app}\vcruntime140d.dll"

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Registry]
Root: HKA; Subkey: "Software\Classes\imessage"; ValueType: "string"; ValueData: "URL:iMessage Protocol"; Flags: uninsdeletekey
Root: HKA; Subkey: "Software\Classes\imessage"; ValueType: "string"; ValueName: "URL Protocol"; ValueData: ""
Root: HKA; Subkey: "Software\Classes\imessage\DefaultIcon"; ValueType: "string"; ValueData: "{app}\{#MyAppExeName},0"
Root: HKA; Subkey: "Software\Classes\imessage\shell\open\command"; ValueType: "string"; ValueData: """{app}\{#MyAppExeName}"" ""%1"""

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent
